EXPCLEAN CSECT
         STM    R14,R12,12(R13)     SALVAR REGISTROS
         LR     R3,R15
         USING  EXPCLEAN,R3         R3 COMO BASE
         LA     R12,SAVE            EN R12 SAVEARAEA
         ST     R13,4(R12)          ENCADENAR
         ST     R12,8(R13)          SAVEAREAS
         LR     R13,R12             DIRECCIONAR MI SAVEAREA
         LA     R11,DCBCOMUN        DIRECCIONAR DCB
         USING  IHADCB,R11          Y DSECT
         L      R4,16               EN R4 DIR. CVT
         L      R4,0(R4)            EN R4 DIR. TCB,S
         L      R4,0(R4)            EN R4 DIR. TCB ACTUAL
         L      R4,12(R4)           EN R4 TIOT
         LA     R4,24(R4)           A PRIMERA DD
         SR     R6,R6               LIMPIAR R6
LOOPOPEN DS     0H
         CLI    0(R4),X'00'         LONG. CERO
         BE     FINLOOP             SI A FIN
         IC     R6,0(R4)            EN R6 LONG. A DESPLAZARSE
         CLC    =C'@',4(R4)         ES DD A LIMPIAR
         BNE    SLOOPEX             NO,EXCLUIR
         MVC    DCBDDNAM,4(R4)      MOVER DDNAME A DCB
         OPEN   (DCBCOMUN,(OUTPUT))
         CLOSE  (DCBCOMUN)          CERRAR
         SPACE  2
SLOOPEX  DS     0H
         AR     R4,R6               INDEXAR TIOT
         B      LOOPOPEN            A LOOP OPEN
         SPACE  3
FINLOOP  DS     0H
         LA     R15,0
         L      R13,4(R13)          DIRECCIONAR SAVEREA ANTERIOR
         RETURN (14,12),RC=(15)     DEVOLVER CONTROL
         SPACE  3
*-------------------------***------------------------------------
         PRINT  NOGEN
SAVE     DC     18F'0'
DCBCOMUN DCB    DDNAME=SYSUT1,DSORG=PS,MACRF=(PM),BUFL=32760,BUFNO=1
         DC     CL8' PERGEN '
         LTORG
         DCBD   DSORG=PS
         YREGS
         END
