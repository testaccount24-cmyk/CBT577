         TITLE 'OBTENCION DEL NUMERO DE REGISTROS DEL FICHERO ENTRADA'
IRXNREG  START 0
         YREGS
         DS    0H
         STM   14,12,12(13)
         BALR  12,0
         USING *,12
         LA    14,SAVE
         ST    13,4(14)
         ST    14,8(13)
         LR    13,14
*---------------------------------------------------------------------*
         LA    R5,IRX_SHVBLOCK
         USING SHVBLOCK,R5
         OPEN  (ENTRADA)
*
LEER     GET    ENTRADA
         AP     CONTADOR,=P'1'
         B      LEER
*
FIN      CLOSE  (ENTRADA)
         MVC    NREGS,MASK
         ED     NREGS,CONTADOR
*
         MVI   SHVCODE,C'S'
         MVC   SHVVALL,=F'16'
         MVC   SHVBUFL,=A(L'NREGS)
         MVC   SHVVALA,=A(NREGS)
         MVC   VN,=C'NREGS '
         BAL   R14,SUBVNL
         ST    R0,SHVNAML
         MVC   SHVNAMA,=A(VN)
         LINK  EP=IRXEXCOM,PARAM=(IRX_IRXEXCOM,0,0,IRX_SHVBLOCK),VL=1
*
         L     13,4(13)
         RETURN (14,12),RC=0
*
ENTRADA  DCB   DSORG=PS,MACRF=GL,DDNAME=ENTRADA,EODAD=FIN
*
SAVE     DS    18F
CONTADOR DC    PL8'0'
NREGS    DC    CL16' '
MASK     DC    X'40202020202020202020202020202020'
*
SUBVNL   DS    0H
         LA    R1,L'VN
         SR    R0,R0
         LA    R15,VN
SUBVNL1  CLI   0(R15),C' '
         BER   R14
         AH    R0,=H'1'
         LA    R15,1(R15)
         BCT   R1,SUBVNL1
         BR    R14
*-----------------------------------------------------------
VN       DS   2CL18
VL       DS   F
*
         DC   C'PERGEN'
         LTORG
IRX_IRXEXCOM DC CL8'IRXEXCOM'
IRX_SHVBLOCK DC (SHVBLEN)X'0'
         IRXSHVB
         END
